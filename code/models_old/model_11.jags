model {
	diffcoef[1] = -0.5
	diffcoef[2] =  0.5

	alpha   ~ dgamma(2,2)
	alphasd ~ dexp(2)
	beta    ~ dbeta(2,2)T(0.001, 0.999)
	betasd  ~ dexp(2)
	tau     ~ dnorm(0.5, 1/0.2^2)T(0.001, 2.99)
	tausd   ~ dexp(2)

	for (c in 1:3) {
		deltacondmid[c] ~ dnorm(0, 1/1.5^2)
		deltaconddiff[c] ~ dnorm(0, 1/0.8^2)

		for (raceCond in 1:2) {
			delta[c,raceCond] = deltacondmid[c] + diffcoef[raceCond]*deltaconddiff[c]
	
			# Group SD
			deltasd[c, raceCond] ~ dexp(2)
		}
	}


	# Subject level parameters
	for (s in 1:nAllSubj) {
		tausubj[s]   ~ dnorm(tau, 1/tausd^2)T(0.001,2.99)
		betasubj[s]  ~ dnorm(beta, 1/betasd^2)T(0.001,0.999)
		alphasubj[s] ~ dnorm(alpha, 1/alphasd^2)T(0.001,)

		for (c in 1:3) {
			for (raceCond in 1:2) {
				deltasubj[s, c, raceCond] ~ dnorm(delta[c, raceCond], 1/deltasd[c,raceCond]^2)
			}
		}
	}


	# Run level parameters
	for (s in 1:nAllSubj) {
		tausubjsd[s] ~ dexp(2)
		betasubjsd[s] ~ dexp(4)
		deltasubjsd[s] ~ dexp(2)

		for (r in 1:nRuns) {
			tausubjrun[s,r] ~ dnorm(tausubj[s], 1/tausubjsd[s]^2)T(0.001,2.99)
			betasubjrun[s,r] ~ dnorm(betasubj[s], 1/betasubjsd[s]^2)T(0.001,0.999)
			for (c in 1:3) {
				for (raceCond in 1:2) {
					deltasubjrun[s, r, c, raceCond] ~ dnorm(deltasubj[s, c, raceCond], 1/deltasubjsd[s]^2)
				}
			}
			
			tausubjrunsd[s,r] ~ dexp(2)
			betasubjrunsd[s,r] ~ dexp(4)
			deltasubjrunsd[s,r] ~ dexp(2)
			for (b in 1:nBlocks) {
				tausubjrunblock[s,r,b] ~ dnorm(tausubjrun[s,r], 1/tausubjrunsd[s,r]^2)T(0.001, 2.99)
				betasubjrunblock[s,r,b] ~ dnorm(betasubjrun[s,r], 1/betasubjrunsd[s,r]^2)T(0.001, 0.999)

				tausubjrunblocksd[s,r,b] ~ dexp(2)

				## Trial level
				for (t in 1:nTrials) {
					tausubjrunblocktrial[s,r,b,t] ~ dnorm(tausubjrunblock[s,r,b], 1/tausubjrunblocksd[s,r,b]^2)T(0.001, 2.99)
				}
					
				for (c in 1:3) {
					for (raceCond in 1:2) {
						deltasubjrunblock[s, r, b, c, raceCond, 1] ~ dnorm(deltasubjrun[s, r, c, raceCond], 1/deltasubjrunsd[s,r]^2)
						deltasubjrunblock[s, r, b, c, raceCond, 2] = 0
				}
			}

			}

		}
	}


	##### Main update loop
	for (s in 1:nAllSubj) {
		for (r in 1:nRuns) {
			for (b in 1:nBlocks) {
				for (tr in 1:nTrials) {
         			y[s,r,b,tr] ~ dwiener(alphasubj[s], tausubjrunblocktrial[s,r,b,tr], betasubjrunblock[s,r,b], deltasubjrunblock[s,r, b,condList[s,r,b,tr],condRaceList[s,r,b,tr],z[s,r,b,tr]+1])
					ypred[s,r,b,tr] ~ dwiener(alphasubj[s], tausubjrunblocktrial[s,r,b,tr], betasubjrunblock[s,r,b], deltasubjrunblock[s, r, b,condList[s,r,b,tr],condRaceList[s,r,b,tr],z[s,r,b,tr]+1])

         			z[s,r,b,tr] ~ dbern(phi[s]) # guessing or not
				}
			}
		}
		phi[s] ~ dunif(0,1)
	}
}
